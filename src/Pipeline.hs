module Pipeline where

import Instr

-- 4 stage pipeline, allowing for 4 instructions (at different stages) to be
-- processed at the same time: fetching, decoding, executing, and write-back.
data Pipeline = Pipeline {
    fetched  :: Maybe Instr
  , decoded  :: Maybe Instr
  , executed :: Maybe WriteBackInstr
} deriving (Show)

-- Return pipeline with nothing in each stage.
empty :: Pipeline
empty = Pipeline Nothing Nothing Nothing

-- Generated by execution step of pipeline.
-- Instruction to machine of values to update.
data WriteBackInstr
    = WriteReg RegIdx Val
    | WriteMem Addr Val
    | WritePrint String
    | NoOp
    | Terminate
    deriving (Show)

type Decoder  m = Instr -> m Instr
type Executer m = Instr -> m WriteBackInstr
type Writer m a = WriteBackInstr -> m a

-- Helper for advance.
op :: (Monad m) => (a -> m b) -> Maybe a ->  m (Maybe b)
op f = maybe (return Nothing) (fmap Just . f)

-- Supplies new instruction into pipleine, and shifts in-flight instructions
-- through pipeline. Returns write-back result, and new state of pipeline.
advance :: (Monad m) => Maybe Instr -> Decoder m -> Executer m -> Writer m a -> Pipeline -> m (Maybe a, Pipeline)
advance fetch decode exec write p = do
    d <- op decode (fetched p)
    e <- op exec   (decoded p)
    w <- op write  (executed p)
    return (w, Pipeline fetch d e)
